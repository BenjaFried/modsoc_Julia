{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "b82ded50",
   "metadata": {},
   "source": [
    "## 1. Packages"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "55d99a65",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Agents\n",
    "using GLMakie\n",
    "using Random\n",
    "using LinearAlgebra\n",
    "using Colors\n",
    "using StaticArrays"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9d3b0d8e",
   "metadata": {},
   "source": [
    "## 2. Agent definition \n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "1b171588",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Define the Particle agent over a continuous 2D space\n",
    "@agent struct Particle(ContinuousAgent{2,Float64})\n",
    "    scale::Float64                   # Visual scale/size\n",
    "    color::RGB{Float64}              # Drawing color\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "eb96f307",
   "metadata": {},
   "source": [
    "## 3. Model initialization "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "47f513eb",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "initialize_model (generic function with 1 method)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function initialize_model(; n_particle=100,\n",
    "    speed=1.0,\n",
    "    whimsy=0,\n",
    "    scale=0.7,\n",
    "    extent=(100, 100),\n",
    "    flocking=0.0,\n",
    "    vision_radius=5.0)\n",
    "    # Continuous 2D toroidal space\n",
    "    space2d = ContinuousSpace(extent; spacing=1, periodic=true) # Periodic boundary conditions  # PARAM\n",
    "\n",
    "    # Global model settings\n",
    "    properties = Dict(\n",
    "        :collisions => 0,               # Total collision count\n",
    "        :whimsy => whimsy,              # Random turn parameter\n",
    "        :speed => speed,                # Movement speed\n",
    "        :flocking => flocking,          # Alignment strength\n",
    "        :vision_radius => vision_radius # Neighbor radius\n",
    "    )\n",
    "\n",
    "    # Build the model\n",
    "    model = StandardABM(\n",
    "        Particle, space2d;\n",
    "        properties=properties, agent_step!,\n",
    "        scheduler=Schedulers.Randomly()\n",
    "    )\n",
    "\n",
    "    # Add each particle with random position, velocity, and color\n",
    "\n",
    "    for _ in 1:n_particle\n",
    "        pos = (rand() * extent[1], rand() * extent[2])\n",
    "        θ = 2π * rand() # Random angle for velocity\n",
    "        vel = SVector(cos(θ), sin(θ)) # Initial velocity as unit vector\n",
    "        vel = vel / norm(vel)                         # Normalize to unit vector\n",
    "        col = RGB(rand(), rand(), rand())             # Random RGB color\n",
    "        add_agent!(pos, model, vel, scale, col)\n",
    "    end\n",
    "\n",
    "    return model\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "570b15bd",
   "metadata": {},
   "source": [
    "## 4. Dynamics - agent_step!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "d05dbeab",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "agent_step! (generic function with 1 method)"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function agent_step!(particle, model)\n",
    "    if model.flocking > 0\n",
    "        acc_vel = SVector(0.0, 0.0)\n",
    "        count_n = 0\n",
    "\n",
    "        # Sum velocities of neighbors within vision_radius\n",
    "        for nb in nearby_agents(particle, model, model.vision_radius)\n",
    "            acc_vel += nb.vel\n",
    "            count_n += 1\n",
    "        end\n",
    "        # Compute average heading if neighbors found\n",
    "        if count_n > 0\n",
    "            avg_vel = acc_vel / count_n                    # Mean heading\n",
    "            nv = norm(avg_vel)\n",
    "            if nv > 0\n",
    "                dir = avg_vel / nv                         # Unit mean direction\n",
    "                # Blend current vel with flock direction\n",
    "                blend = (1 - model.flocking) * particle.vel .+ model.flocking * dir\n",
    "                bn = norm(blend)\n",
    "                if bn > 0\n",
    "                    particle.vel = blend / bn              # Update heading\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    Δθ = (rand() * model.whimsy - rand() * model.whimsy) * (π / 180) # Random turn in radians\n",
    "    θ0 = atan(particle.vel[2], particle.vel[1])           # Current heading angle\n",
    "    θ1 = θ0 + Δθ                                          # New heading angle\n",
    "    particle.vel = SVector(cos(θ1), sin(θ1))              # Apply turn\n",
    "\n",
    "\n",
    "    # Move particle forward\n",
    "    move_agent!(particle, model, model.speed)\n",
    "\n",
    "    # Check for collisions with nearby agents\n",
    "    neighbors = collect(nearby_agents(particle, model, 1))\n",
    "    if !isempty(neighbors)\n",
    "        # scatter self\n",
    "        θ = (π / 180) * rand(0:359)\n",
    "        particle.vel = SVector(cos(θ), sin(θ))\n",
    "        move_agent!(particle, model, 0.5)  # Scatter self\n",
    "\n",
    "        # scatter all neighbors in-radius\n",
    "        for nb in neighbors\n",
    "            θn = (π / 180) * rand(0:359)\n",
    "            nb.vel = SVector(cos(θn), sin(θn))\n",
    "            move_agent!(nb, model, 0.5)\n",
    "        end\n",
    "\n",
    "        model.collisions += 1\n",
    "    end\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "27075320",
   "metadata": {},
   "source": [
    "## 5. GUI visualization helper\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "d739d838",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "particle_marker (generic function with 1 method)"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "agent_color(p::Particle) = p.color\n",
    "\n",
    "function particle_marker(b::Particle)\n",
    "    scale_factor = 1 * b.scale\n",
    "    # Define triangle vertices: back-left, front, back-rightol\n",
    "    particle_polygon = Makie.Polygon(Point2f[\n",
    "        (-scale_factor, -scale_factor),   # Back left vertex\n",
    "        (2 * scale_factor, 0),            # Front vertex (pointing forward)\n",
    "        (-scale_factor, scale_factor)     # Back right vertex\n",
    "    ])\n",
    "\n",
    "    # Rotate the triangle to match particle's heading\n",
    "    φ = atan(b.vel[2], b.vel[1])\n",
    "    rotate_polygon(particle_polygon, φ)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9885f2ab",
   "metadata": {},
   "source": [
    "## 6. Launch GUI & controls "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "21dd8387",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "GLMakie.Screen(...)"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "params = Dict(\n",
    "    :speed => 0.1:0.1:2.0,            # Movement speed            # PARAM\n",
    "    :whimsy => 0.0:10.0:359.0,        # Turning range             # PARAM\n",
    "    :flocking => 0.0:0.1:1.0,         # Alignment strength        # PARAM\n",
    "    :vision_radius => 1.0:1.0:20.0    # Neighbor detection radius # PARAM\n",
    ")\n",
    "\n",
    "# Run GUI\n",
    "fig, _ = abmexploration(\n",
    "    initialize_model(; n_particle=100, # PARAM\n",
    "        scale=0.7,                         # PARAM\n",
    "        extent=(100, 100));                # PARAM\n",
    "    agent_color=agent_color,\n",
    "    agent_marker=particle_marker,\n",
    "    params=params,\n",
    "    mdata=[:collisions],  # Show total collisions over time\n",
    "    mlabels=[\"Collisions\"],\n",
    "    enable_inspection=false           # PARAM\n",
    ")\n",
    "display(fig)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
