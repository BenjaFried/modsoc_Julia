{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "13bb246f",
   "metadata": {},
   "source": [
    "## 1. Packages"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dabb5efe",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Agents\n",
    "using GLMakie\n",
    "using Distributions\n",
    "using Measures\n",
    "using Statistics\n",
    "using Random"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4a6fba07",
   "metadata": {},
   "source": [
    "## 2. Agent definition"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "077b1765",
   "metadata": {},
   "outputs": [],
   "source": [
    "@agent struct SchellingAgent(GridAgent{2}) # Agent on a 2D grid (adapted from Agents.jl)\n",
    "    mood::Bool\n",
    "    group::Int\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "09262479",
   "metadata": {},
   "source": [
    "## 3. Model initialization "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "cae73acc",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "initialize (generic function with 1 method)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "\n",
    "function initialize(; M::Int=10,      # Grid size (MxM)\n",
    "    min_to_be_happy::Float64=0.5,     # Minimum fraction of similar neighbors to be happy\n",
    "    ρ::Float64=0.9,                   # Density of agents on grid\n",
    "    proportion_red::Float64=0.5,      # Proportion of red agents (group 1)\n",
    "    lonely_agents_unhappy::Bool=true) # Whether agents with no neighbors are unhappy\n",
    "\n",
    "\n",
    "    gridsize = (M, M)\n",
    "    space = GridSpaceSingle(gridsize; periodic=true, metric=:chebyshev) # Toroidal grid # PARAM\n",
    "    total_agents = round(Int, ρ * prod(gridsize))  # Calculate number of agents based on density\n",
    "\n",
    "    properties = Dict(\n",
    "        :min_to_be_happy => min_to_be_happy,\n",
    "        :M => M,\n",
    "        :ρ => ρ,\n",
    "        :total_agents => total_agents,\n",
    "        :lonely_agents_unhappy => lonely_agents_unhappy,\n",
    "        :average_similarity => 0.0,\n",
    "    )\n",
    "\n",
    "    # Build model\n",
    "    model = StandardABM(\n",
    "        SchellingAgent, space;\n",
    "        agent_step!,                    # Agent stepping function\n",
    "        model_step!,                    # Model stepping function\n",
    "        properties=properties,\n",
    "        scheduler=Schedulers.Randomly() # Random agent activation order\n",
    "    )\n",
    "\n",
    "    # Add agents to random positions on the grid\n",
    "    for _ in 1:total_agents\n",
    "        add_agent_single!(\n",
    "            model;\n",
    "            mood=false,                            # All agents start unhappy\n",
    "            group=rand() < proportion_red ? 1 : 2  # Randomly assign to group 1 (red) or 2 (blue)\n",
    "        )\n",
    "    end\n",
    "\n",
    "    return model\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "250f3079",
   "metadata": {},
   "source": [
    "## 4. Dynamics - agent_step! - model_step!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "c63e13d7",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "model_step! (generic function with 1 method)"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "\n",
    "# Define the stepping function for each agent\n",
    "function agent_step!(agent::SchellingAgent, model)\n",
    "    minhappy = model.min_to_be_happy\n",
    "    nbrs = collect(nearby_agents(agent, model))  # Get all neighboring agents\n",
    "\n",
    "    # Handle case where agent has no neighbors\n",
    "    if isempty(nbrs)\n",
    "        if model.lonely_agents_unhappy\n",
    "            agent.mood = false               # Lonely agents are unhappy\n",
    "            move_agent_single!(agent, model) # Move to a random empty cell\n",
    "        else\n",
    "            agent.mood = true                # Lonely agents are content\n",
    "        end\n",
    "        return\n",
    "    end\n",
    "\n",
    "    # Calculate fraction of neighbors that are same group\n",
    "    same = count(n -> n.group == agent.group, nbrs)\n",
    "    frac = same / length(nbrs)\n",
    "\n",
    "    # Update mood and move if unhappy\n",
    "    if frac ≥ minhappy # \\geq + tab =  ≥\n",
    "        agent.mood = true                 # Agent is happy with neighbors\n",
    "    else\n",
    "        agent.mood = false                # Agent is unhappy\n",
    "        move_agent_single!(agent, model)  # Move to random empty cell\n",
    "    end\n",
    "end\n",
    "\n",
    "function model_step!(model)\n",
    "    # Calculate average similarity of agents with their neighbors\n",
    "    model.average_similarity = calculate_average_similarity(model)\n",
    "end\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b56c55f2",
   "metadata": {},
   "source": [
    "## 5. Helper - measurement\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "ffb49351",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "calculate_average_similarity (generic function with 1 method)"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Calculate the average similarity across all agents with neighbors\n",
    "function calculate_average_similarity(model)\n",
    "    sims = Float64[]\n",
    "    for a in allagents(model)\n",
    "        nbrs = collect(nearby_agents(a, model)) # Get neighbors\n",
    "        if isempty(nbrs)\n",
    "            push!(sims, 1.0) # If no neighbors, consider similarity as 1 (or could be 0 based on interpretation)\n",
    "        else\n",
    "            same = count(n -> n.group == a.group, nbrs) # Count similar neighbors\n",
    "            push!(sims, same / length(nbrs))  # Add fraction of similar neighbors\n",
    "        end\n",
    "    end\n",
    "    return isempty(sims) ? 0.0 : mean(sims)  # Return mean similarity or 0 if no data\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8f64282d",
   "metadata": {},
   "source": [
    "## 6. GUI setup\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dcebb40e",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "setup_gui (generic function with 1 method)"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "\n",
    "# Define color mapping for visualization\n",
    "function set_color(a::SchellingAgent)\n",
    "    return a.group == 1 ? :red : :blue  # Group 1 is red, Group 2 is blue\n",
    "end\n",
    "\n",
    "# Set up GUI \n",
    "function setup_gui(; lonely_agents_unhappy::Bool=true, M=60, ρ=0.8, proportion_red=0.5)\n",
    "\n",
    "    # Parameter slider for min_to_be_happy threshold\n",
    "    params = Dict(:min_to_be_happy => 0.1:0.01:0.9)\n",
    "\n",
    "\n",
    "    fig, _ = abmexploration(\n",
    "        initialize(M=M, ρ=ρ, min_to_be_happy=0.1,\n",
    "            proportion_red=0.5,\n",
    "            lonely_agents_unhappy=lonely_agents_unhappy);\n",
    "        agent_color=set_color,           # Color agents by group\n",
    "        mdata=[(:average_similarity)],   # Dynamic plot of the average similarity\n",
    "        mlabels=[\"Average Similarity\"],  # Label for the dynamic plot\n",
    "        params\n",
    "    )\n",
    "    return fig\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e3a9011a",
   "metadata": {},
   "source": [
    "## 7. Launch GUI & Controls"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "ce7da222",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "GLMakie.Screen(...)"
      ]
     },
     "execution_count": 7,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Run GUI \n",
    "fig_interactive = setup_gui(; lonely_agents_unhappy=true, M=60, ρ=0.8, proportion_red=0.5) # PARAM\n",
    "display(fig_interactive)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
