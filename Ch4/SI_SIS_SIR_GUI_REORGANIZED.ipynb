{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "b4a3c617",
   "metadata": {},
   "source": [
    "## 1. Packages"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "d6cdc340",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Agents\n",
    "using GLMakie\n",
    "using Random\n",
    "using LinearAlgebra\n",
    "using StaticArrays\n",
    "using Distributions\n",
    "using OrderedCollections: OrderedDict"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1a7496d6",
   "metadata": {},
   "source": [
    "## 2. Agent definition"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "521b16e1",
   "metadata": {},
   "outputs": [],
   "source": [
    "@agent struct Agent(ContinuousAgent{2,Float64}) # Agent in 2D continuous space\n",
    "    infected::Bool\n",
    "    recovered::Bool\n",
    "    transmission_rate::Float64\n",
    "    recovery_rate::Float64\n",
    "    speed::Float64\n",
    "    scale::Float64\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "718bdbef",
   "metadata": {},
   "source": [
    "## 3. Helper - random unit vector\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bd4fe35d",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "random_unit_vec (generic function with 1 method)"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Random unit velocity in 2D.\n",
    "random_unit_vec() = begin\n",
    "    v = rand(2) .- 0.5 # Random vector \n",
    "    v ./= max(norm(v)) # Normalize to unit vector\n",
    "    v = SVector{2,Float64}(v) # Convert to static vector for speed\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "36fc4c41",
   "metadata": {},
   "source": [
    "## 4. Dynamics - agent_step! - model_step!\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "00dadb33",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "agent_step! (generic function with 1 method)"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function agent_step!(a, model)\n",
    "    dm = model.disease_model_index\n",
    "    susceptible = !a.infected && !(dm == 3 && a.recovered) # Susceptible if not infected and (not recovered if SIR)\n",
    "    if susceptible\n",
    "        k = 0 # Count of infected neighbors\n",
    "        for n in nearby_agents(a, model, 1.0) # Count infected neighbors within radius 1\n",
    "            if n.infected == true\n",
    "                k += 1 # Increment count of infected neighbors\n",
    "            end\n",
    "        end\n",
    "        p_inf = 1 - ((1 - model.transmission_rate)^k) * (1 - model.spontaneous_infect) # Probability of infection from neighbors and spontaneous infection\n",
    "        if rand() < p_inf\n",
    "            a.infected = true\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # move agent \n",
    "    θmax = deg2rad(model.turning_angle) # Maximum turning angle in radians\n",
    "    δθ = (rand() - rand()) * θmax       # Random angle \n",
    "    ϕ = atan(a.vel[2], a.vel[1]) + δθ   # Current direction + angle change\n",
    "    a.vel = @SVector [cos(ϕ), sin(ϕ)]   # New velocity vector\n",
    "    move_agent!(a, model, model.speed)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a488a324",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "model_step! (generic function with 1 method)"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function model_step!(model)\n",
    "    dm = model.disease_model_index\n",
    "    infected_start = Set(a.id for a in allagents(model) if a.infected) # IDs of currently infected agents\n",
    "\n",
    "    if dm == 2  # SIS\n",
    "        for id in infected_start\n",
    "            a = model[id]\n",
    "            if a.infected && rand() < model.recovery_rate # Chance of recovery back to susceptible\n",
    "                a.infected = false\n",
    "                a.recovered = false\n",
    "            end\n",
    "        end\n",
    "    elseif dm == 3 # SIR \n",
    "        for id in infected_start\n",
    "            a = model[id]\n",
    "            if a.infected && rand() < model.recovery_rate # Chance of recovery to recovered state\n",
    "                a.infected = false\n",
    "                a.recovered = true\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    inf_frac = count(a -> a.infected, allagents(model)) / nagents(model) # Calculate fraction of infected agents\n",
    "\n",
    "    if inf_frac > model.max_infected_fraction # Update max infected fraction if current fraction exceeds it\n",
    "        model.max_infected_fraction = inf_frac\n",
    "    end\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ccab40b8",
   "metadata": {},
   "source": [
    "## 5. Model initialization"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "f8a9affd",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "initialize_model (generic function with 1 method)"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function initialize_model(; n_agents::Int=500,\n",
    "    transmission_rate::Float64=0.10,\n",
    "    recovery_rate::Float64=0.01,\n",
    "    spontaneous_infect::Float64=0.0,\n",
    "    speed::Float64=1.0,\n",
    "    scale::Float64=0.7,\n",
    "    extent::Tuple{<:Real,<:Real}=(100, 100), # Size of the 2D space\n",
    "    turning_angle::Float64=360.0,\n",
    "    disease_model::Symbol=:SI,               # :SI, :SIS, :SIR\n",
    "    initial_infected::Int=1)\n",
    "\n",
    "    space = ContinuousSpace(extent; spacing=1, periodic=true)\n",
    "\n",
    "    model = StandardABM(\n",
    "        Agent, space;\n",
    "        properties=Dict(\n",
    "            :turning_angle => turning_angle,\n",
    "            :disease_model_index => (disease_model === :SI ? 1 : disease_model === :SIS ? 2 : 3), # Map model symbol to index\n",
    "            :max_infected_fraction => 0.0,\n",
    "            :speed => speed,\n",
    "            :transmission_rate => transmission_rate,\n",
    "            :recovery_rate => recovery_rate,\n",
    "            :spontaneous_infect => spontaneous_infect,\n",
    "        ),\n",
    "        agent_step!, model_step!,\n",
    "        scheduler=Schedulers.Randomly()\n",
    "    )\n",
    "    # Add agents with random positions and initial infection state\n",
    "    for i in 1:n_agents\n",
    "        pos = (rand() * extent[1], rand() * extent[2]) #\n",
    "        infected = i ≤ initial_infected\n",
    "        recovered = false\n",
    "        a = add_agent!(model, pos, infected, recovered,\n",
    "            transmission_rate, recovery_rate, speed, scale)\n",
    "        a.vel = random_unit_vec()\n",
    "    end\n",
    "    return model\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2fff505b",
   "metadata": {},
   "source": [
    "## 6. GUI setup"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e9aa4149",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Red for infected, blue for recovered, black for susceptible\n",
    "agent_color(a::Agent) = a.infected ? :red : (a.recovered ? :blue : :black)\n",
    "\n",
    "# Time series to show: S/I/R fractions & running max infected fraction\n",
    "mdata = [\n",
    "    m -> count(a -> !a.infected && !a.recovered, allagents(m)) / nagents(m),\n",
    "    m -> count(a -> a.infected, allagents(m)) / nagents(m),\n",
    "    m -> count(a -> a.recovered, allagents(m)) / nagents(m),\n",
    "    m -> m.max_infected_fraction,\n",
    "]\n",
    "mlabels = [\"Susceptible fraction\", \"Infected fraction\", \"Recovered fraction\", \"Max infected fraction\"]\n",
    "\n",
    "# Sliders in GUI\n",
    "params = OrderedDict(\n",
    "    :disease_model_index => 1:1:3, # 1=SI, 2=SIS, 3=SIR\n",
    "    :speed => 0.1:0.1:5.0,\n",
    "    :transmission_rate => 0.0:0.01:1.0,\n",
    "    :recovery_rate => 0.0:0.01:1.0,\n",
    "    :spontaneous_infect => 0.0:0.01:1.0,\n",
    "    :turning_angle => 0.0:15.0:360.0,\n",
    ");\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4cfb1d2c",
   "metadata": {},
   "source": [
    "## 7. GUI visualization helper"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "4f7a701e",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "particle_marker (generic function with 1 method)"
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "\n",
    "# Triangle marker rotated by velocity direction.\n",
    "function particle_marker(b::Agent)\n",
    "    scale_factor = 1 * b.scale\n",
    "    particle_polygon = Makie.Polygon(Point2f[ # Custom shape\n",
    "        (-scale_factor, -scale_factor),\n",
    "        (2 * scale_factor, 0),\n",
    "        (-scale_factor, scale_factor)\n",
    "    ])\n",
    "    φ = atan(b.vel[2], b.vel[1])\n",
    "    rotate_polygon(particle_polygon, φ)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "22f5660f",
   "metadata": {},
   "source": [
    "## 8. GUI & controls"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "892fe735",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "GLMakie.Screen(...)"
      ]
     },
     "execution_count": 9,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Run GUI \n",
    "fig, abmobs = abmexploration(\n",
    "    initialize_model(n_agents=500, disease_model=:SI); # n_agents PARAM \n",
    "    agent_color=agent_color,\n",
    "    agent_marker=particle_marker,\n",
    "    mdata=mdata,\n",
    "    mlabels=mlabels,\n",
    "    params=params,\n",
    "    agentsplotkwargs=(; inspectable=false),\n",
    "    mplotkwargs=(; inspectable=false)\n",
    ")\n",
    "display(fig)\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
