{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "04140ca7",
   "metadata": {},
   "source": [
    "## 1. Packages"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "52ebda67",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Agents\n",
    "using GLMakie\n",
    "using Observables\n",
    "using Random\n",
    "using ColorSchemes"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d96c414a",
   "metadata": {},
   "source": [
    "## 2. Agent definition"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 24,
   "id": "9f91abf6",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Define Agent Type\n",
    "@agent struct OP(GridAgent{2})\n",
    "    opinion::Float64\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "dcf415f6",
   "metadata": {},
   "source": [
    "## 3. Model initialization "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "438647ad",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "initialize (generic function with 1 method)"
      ]
     },
     "execution_count": 25,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Initalize Model\n",
    "function initialize(; M=10,\n",
    "    local_int::Bool=false,\n",
    "    learning_rate::Float64=0.5,\n",
    "    consensus_threshold::Float64=0.05,\n",
    "    interaction_mode::Symbol=:positive,\n",
    "    confidence_threshold::Float64=0.10)\n",
    "\n",
    "    space = GridSpaceSingle((M, M); periodic=true, metric=:manhattan)\n",
    "\n",
    "    props = Dict(\n",
    "        :local_int => local_int,\n",
    "        :learning_rate => learning_rate,\n",
    "        :consensus_threshold => consensus_threshold,\n",
    "        :interaction_mode => interaction_mode,\n",
    "        :confidence_threshold => confidence_threshold,\n",
    "        :consensus => false,\n",
    "        :time_to_consensus => 0,\n",
    "    )\n",
    "\n",
    "    model = StandardABM(\n",
    "        OP, space;\n",
    "        properties=props,\n",
    "        model_step!,\n",
    "        scheduler=Schedulers.Randomly(),\n",
    "    )\n",
    "\n",
    "    for _ in 1:(M^2) # Fill grid completely\n",
    "        add_agent_single!(model; opinion=2rand(abmrng(model)) - 1) # Opinion in [-1, 1]\n",
    "    end\n",
    "    return model\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b121c007",
   "metadata": {},
   "source": [
    "## 4. Helper - sample two distinct agents"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a0dc8c67",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "two_distinct_ids (generic function with 1 method)"
      ]
     },
     "execution_count": 26,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Get Two Distinct Agent IDs \n",
    "@inline function two_distinct_ids(n::Int)\n",
    "    i = rand(1:n)\n",
    "    j = rand(1:(n-1))\n",
    "    j >= i && (j += 1)\n",
    "    return i, j\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "cb0f0b45",
   "metadata": {},
   "source": [
    "## 5. Interaction mechanism "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d0ee0da5",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "interact! (generic function with 1 method)"
      ]
     },
     "execution_count": 27,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Interaction between two agents based on the selected mechanism\n",
    "function interact!(a::OP, b::OP, model)\n",
    "    x1, x2 = a.opinion, b.opinion # Current opinions\n",
    "    lr = model.learning_rate\n",
    "    d = model.confidence_threshold\n",
    "    mode = model.interaction_mode\n",
    "    Δ = abs(x1 - x2) # Opinion difference \n",
    "\n",
    "    if mode === :positive # Standard positve interaction\n",
    "        x1n = x1 + lr * (x2 - x1)\n",
    "        x2n = x2 + lr * (x1 - x2)\n",
    "\n",
    "    elseif mode === :bounded # Bounded confidence\n",
    "        Δ < d || return\n",
    "        x1n = x1 + lr * (x2 - x1)\n",
    "        x2n = x2 + lr * (x1 - x2)\n",
    "\n",
    "    elseif mode === :negative # Bounded confidence with negative interaction\n",
    "        if Δ < d\n",
    "            x1n = x1 + lr * (x2 - x1)\n",
    "            x2n = x2 + lr * (x1 - x2)\n",
    "\n",
    "        elseif Δ > d\n",
    "            if x1 > x2\n",
    "                x1n = x1 + lr * (x1 - x2) * (1 - x1) * 0.5\n",
    "                x2n = x2 + lr * (x2 - x1) * (1 + x2) * 0.5\n",
    "            else\n",
    "                x1n = x1 + lr * (x1 - x2) * (1 + x1) * 0.5\n",
    "                x2n = x2 + lr * (x2 - x1) * (1 - x2) * 0.5\n",
    "            end\n",
    "        else\n",
    "            # Δ == d: no change\n",
    "            return\n",
    "        end\n",
    "    end\n",
    "\n",
    "\n",
    "    a.opinion = x1n\n",
    "    b.opinion = x2n\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ef13192a",
   "metadata": {},
   "source": [
    "## 6. Helper - clique detection - metrics"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8180c9a4",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "clique_ranges (generic function with 1 method)"
      ]
     },
     "execution_count": 28,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Clique detection and counting functions\n",
    "function clique_ranges(model)::Vector{Float64}\n",
    "    d = model.confidence_threshold\n",
    "    xs = sort!([ag.opinion for ag in allagents(model)]) # Sorted opinions to find ranges\n",
    "    n = length(xs)\n",
    "    n == 0 && return Float64[]\n",
    "    n == 1 && return [0.0]\n",
    "\n",
    "    ranges = Float64[]\n",
    "    cmin = xs[1]\n",
    "    cmax = xs[1]\n",
    "    prev = xs[1]\n",
    "\n",
    "    # Iterate through sorted opinions and find ranges\n",
    "    @inbounds for k in 2:n\n",
    "        x = xs[k]\n",
    "        if (x - prev) > d\n",
    "            push!(ranges, cmax - cmin)\n",
    "            cmin = x\n",
    "            cmax = x\n",
    "        else\n",
    "            cmax = x\n",
    "        end\n",
    "        prev = x\n",
    "    end\n",
    "    push!(ranges, cmax - cmin)\n",
    "    return ranges\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4d1211a5",
   "metadata": {},
   "source": [
    "## 7. Dynamics - model_step!"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4398fb88",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "model_step! (generic function with 1 method)"
      ]
     },
     "execution_count": 29,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function model_step!(model)\n",
    "    n = nagents(model)\n",
    "    n <= 1 && return\n",
    "\n",
    "    # Select two agents to interact (global or local-neighborhood)\n",
    "    if model.local_int\n",
    "        i = rand(1:n)\n",
    "        a = model[i]\n",
    "        j = random_nearby_id(a, model, 1)\n",
    "        b = model[j]\n",
    "    else\n",
    "        i, j = two_distinct_ids(n)\n",
    "        a = model[i]\n",
    "        b = model[j]\n",
    "    end\n",
    "\n",
    "    interact!(a, b, model)\n",
    "\n",
    "    # Consensus check: all clique opinion ranges below consensus_threshold\n",
    "    ranges = clique_ranges(model)\n",
    "    if !isempty(ranges) && all(r -> r < model.consensus_threshold, ranges) && !model.consensus\n",
    "        model.time_to_consensus = abmtime(model)\n",
    "        model.consensus = true\n",
    "    end\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0d564926",
   "metadata": {},
   "source": [
    "## 8. GUI setup "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "af881f86",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "ObserverFunction defined at In[30]:190 operating on Observable{Any}(0.05)"
      ]
     },
     "execution_count": 30,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Visualization setup\n",
    "\n",
    "opinion_cmap = cgrad(:RdBu, rev=true)  # blue at -1, red at +1\n",
    "\n",
    "# push slider values away from the extremes by \"pad\"\n",
    "remap_t(t; pad=0.12) = pad + (1 - 2pad) * clamp(t, 0, 1)\n",
    "\n",
    "agent_color(a::OP) = get(opinion_cmap, remap_t((a.opinion + 1) / 2; pad=0.15))\n",
    "\n",
    "# Custom GUI + dynamic plots\n",
    "GLMakie.activate!()\n",
    "\n",
    "init_base = (M=10, local_int=false)\n",
    "init_rate = 0.5\n",
    "init_thr = 0.10\n",
    "init_cons = 0.05\n",
    "init_mode = :positive\n",
    "\n",
    "model = initialize(; init_base..., learning_rate=init_rate,\n",
    "    consensus_threshold=init_cons,\n",
    "    confidence_threshold=init_thr, interaction_mode=init_mode)\n",
    "\n",
    "fig = Figure(size=(1350, 740))\n",
    "\n",
    "# Layout\n",
    "ax_grid = fig[1, 1] = Axis(fig; title=\"Agents on grid\")\n",
    "ax_lines = fig[1, 2] = Axis(fig; title=\"All opinions over time\",\n",
    "    xlabel=\"step\", ylabel=\"opinion\")\n",
    "controls = fig[1, 3] = GridLayout()\n",
    "\n",
    "# controls layout\n",
    "colsize!(fig.layout, 3, Fixed(280))               # sidebar width\n",
    "colgap!(fig.layout, 20);\n",
    "rowgap!(fig.layout, 10);  # spacing between columns/rows\n",
    "\n",
    "# ABM view\n",
    "abmobs = abmplot!(ax_grid, model;\n",
    "    agent_color=agent_color,\n",
    "    agent_marker=:rect,\n",
    "    agent_size=30,\n",
    "    add_controls=false,\n",
    "    enable_inspection=false)\n",
    "\n",
    "# spaghetti plot\n",
    "limits!(ax_lines, (nothing, nothing), (-1, 1))\n",
    "traces = IdDict{Int,Observable{Vector{Point2f}}}()\n",
    "\n",
    "function rebuild_spaghetti!(m)\n",
    "    empty!(traces)\n",
    "    empty!(ax_lines)\n",
    "    t0 = Float32(abmtime(m))\n",
    "    for ag in allagents(m)\n",
    "        obs = Observable([Point2f(t0, Float32(ag.opinion))])\n",
    "        lines!(ax_lines, obs; linewidth=1, transparency=true)\n",
    "        traces[ag.id] = obs\n",
    "    end\n",
    "    autolimits!(ax_lines)\n",
    "end\n",
    "rebuild_spaghetti!(abmobs.model[])\n",
    "\n",
    "last_step = Ref(Int(abmtime(abmobs.model[])))\n",
    "on(abmobs.model) do m\n",
    "    t = Float32(abmtime(m))\n",
    "    if Int(t) <= last_step[]           # reset check\n",
    "        rebuild_spaghetti!(m)\n",
    "    else\n",
    "        for ag in allagents(m)\n",
    "            obs = traces[ag.id]\n",
    "            buf = obs[]\n",
    "            push!(buf, Point2f(t, Float32(ag.opinion)))\n",
    "            obs[] = buf\n",
    "        end\n",
    "        reset_limits!(ax_lines)        # recompute X; keep Y fixed\n",
    "    end\n",
    "    last_step[] = Int(t)\n",
    "end\n",
    "\n",
    "# Sidebar controls\n",
    "# Row 1: buttons\n",
    "btn_row = controls[1, 1] = GridLayout()\n",
    "run_btn = Button(btn_row[1, 1], label=\"Run\")\n",
    "step_btn = Button(btn_row[1, 2], label=\"Step\")\n",
    "reset_btn = Button(btn_row[1, 3], label=\"Reset\")\n",
    "\n",
    "# Row 2: sliders (SPU (steps per update) & Sleep (speed))\n",
    "Label(controls[2, 1], \"Simulation\", font=:bold, halign=:left)\n",
    "sg_sim = SliderGrid(\n",
    "    controls[3, 1],\n",
    "    (label=\"SPU\", range=1:50, format=\"{:.0f}\", startvalue=1),\n",
    "    (label=\"Sleep (s)\", range=0.0:0.01:0.3, format=\"{:.02f}s\", startvalue=0.00),\n",
    "    width=260, tellheight=false\n",
    ")\n",
    "\n",
    "spu_slider = sg_sim.sliders[1]\n",
    "sleep_slider = sg_sim.sliders[2]\n",
    "\n",
    "# Row 3: model params (Menu + slidergrid)\n",
    "Label(controls[4, 1], \"Model parameters\", font=:bold, halign=:left)\n",
    "\n",
    "Label(controls[5, 1], \"Mode\", halign=:left)\n",
    "mode_menu = Menu(controls[6, 1],\n",
    "    options=[(\"Positive\", :positive),\n",
    "        (\"Bounded\", :bounded),\n",
    "        (\"Bounded+Negative\", :negative)],\n",
    "    width=260)\n",
    "mode_menu.selection[] = init_mode\n",
    "\n",
    "# Locality control, label + toggle\n",
    "local_row = GridLayout()\n",
    "local_row[1, 1] = Label(fig, \"Local interactions\"; halign=:left)\n",
    "local_toggle = Toggle(fig; active=init_base.local_int)\n",
    "local_row[1, 2] = local_toggle\n",
    "colgap!(local_row, 12)\n",
    "colsize!(local_row, 1, Auto())\n",
    "colsize!(local_row, 2, Fixed(50))\n",
    "\n",
    "controls[7, 1] = local_row\n",
    "\n",
    "\n",
    "# Sliders below locality\n",
    "sg_model = SliderGrid(\n",
    "    controls[8, 1],\n",
    "    (label=\"Learning rate\", range=0.0:0.01:1.0, format=\"{:.02f}\", startvalue=init_rate),\n",
    "    (label=\"Confidence threshold\", range=0.0:0.01:1.0, format=\"{:.02f}\", startvalue=init_thr),\n",
    "    (label=\"Consensus threshold\", range=0.0:0.01:1.0, format=\"{:.02f}\", startvalue=init_cons),\n",
    "    width=260, tellheight=false\n",
    ")\n",
    "\n",
    "lr_slider = sg_model.sliders[1]\n",
    "thr_slider = sg_model.sliders[2]\n",
    "cons_slider = sg_model.sliders[3]\n",
    "\n",
    "# adjust gaps inside the sidebar\n",
    "colgap!(controls, 0);\n",
    "rowgap!(controls, 8);\n",
    "\n",
    "# controls\n",
    "running = Observable(false)\n",
    "on(run_btn.clicks) do _\n",
    "    running[] = !running[]\n",
    "    run_btn.label[] = running[] ? \"Pause\" : \"Run\"\n",
    "end\n",
    "\n",
    "# Step button\n",
    "@async begin\n",
    "    while true\n",
    "        if running[]\n",
    "            Agents.step!(abmobs, Int(round(spu_slider.value[])))\n",
    "\n",
    "            # Stop automatically once consensus is reached\n",
    "            if getproperty(abmobs.model[], :consensus) === true\n",
    "                running[] = false\n",
    "                run_btn.label[] = \"Run\"\n",
    "            end\n",
    "\n",
    "            sleep(sleep_slider.value[])\n",
    "        else\n",
    "            sleep(0.05)\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "# Reset button\n",
    "on(reset_btn.clicks) do _\n",
    "    abmobs.model[] = initialize(; M=init_base.M,\n",
    "        local_int=local_toggle.active[],\n",
    "        learning_rate=Float64(lr_slider.value[]),\n",
    "        consensus_threshold=Float64(cons_slider.value[]),\n",
    "        confidence_threshold=Float64(thr_slider.value[]),\n",
    "        interaction_mode=mode_menu.selection[])\n",
    "    running[] = false\n",
    "    run_btn.label[] = \"Run\"\n",
    "end\n",
    "\n",
    "# update params live when adjusted\n",
    "on(mode_menu.selection) do sym\n",
    "    abmobs.model[].interaction_mode = sym\n",
    "end\n",
    "\n",
    "# locality toggle\n",
    "on(local_toggle.active) do v\n",
    "    abmobs.model[].local_int = v\n",
    "end\n",
    "# sliders\n",
    "on(lr_slider.value) do v\n",
    "    abmobs.model[].learning_rate = Float64(v)\n",
    "end\n",
    "# sliders\n",
    "on(thr_slider.value) do v\n",
    "    abmobs.model[].confidence_threshold = Float64(v)\n",
    "end\n",
    "# sliders\n",
    "on(cons_slider.value) do v\n",
    "    abmobs.model[].consensus_threshold = Float64(v)\n",
    "end\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4999cda1",
   "metadata": {},
   "source": [
    "## 9. Launch GUI"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 31,
   "id": "e017d770",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "GLMakie.Screen(...)"
      ]
     },
     "execution_count": 31,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# Run to open the GUI\n",
    "display(fig)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
